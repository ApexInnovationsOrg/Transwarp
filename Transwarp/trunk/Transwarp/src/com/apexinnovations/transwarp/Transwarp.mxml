<?xml version="1.0" encoding="utf-8"?>
<transwarp:TranswarpBase xmlns:fx="http://ns.adobe.com/mxml/2009" 
			   xmlns:s="library://ns.adobe.com/flex/spark" 
			   xmlns:mx="library://ns.adobe.com/flex/mx" 
			   xmlns:transwarp="com.apexinnovations.transwarp.*"
			   xmlns:ui="com.apexinnovations.transwarp.ui.*"
			   preloader="com.apexinnovations.transwarp.preloader.PreloaderDisplay"
			   xmlns:ext="com.apexinnovations.transwarp.ui.ext.*"
			   addedToStage="onAdded()"
			   applicationComplete="applicationCompleteHandler()"
			   minHeight="738" minWidth="924">
	<fx:Declarations>
		<s:TitleWindow title="Title" id="errorDialog" close="PopUpManager.removePopUp(errorDialog); fscommand('quit');" width="400" height="300">
			<s:TextArea id="infoText" height="100%" width="100%"/> 
		</s:TitleWindow>
	</fx:Declarations>
	
	<fx:Metadata>
		[ResourceBundle("Tooltips")]
		[ResourceBundle("Buttons")]
	</fx:Metadata>
	
	
	<fx:Script>
		<![CDATA[
			import com.apexinnovations.transwarp.data.Courseware;
			import com.apexinnovations.transwarp.data.User;
			import com.apexinnovations.transwarp.utils.TranswarpVersion;
			import com.apexinnovations.transwarp.webservices.*;
			
			import flash.events.*;
			
			import mx.managers.ISystemManager;
			import mx.managers.PopUpManager;
			import mx.resources.ResourceManager;
			
			TranswarpVersion.revision = "$Rev$";
			
			[Bindable] protected var themeColor:uint = 0xcc3337;
			
			protected var manager:TranswarpSystemManager;
			protected var courseware:Courseware;
			
			protected var maximized:Boolean = false;
			protected var currentlyIdle:Boolean = false;
			
			protected var xmlLoadError:Error;
						
			override public function set systemManager(value:ISystemManager):void {
				super.systemManager = value;
				
				manager = value as TranswarpSystemManager;
			
				try {
					courseware = new Courseware(manager.xml);
				} catch(e:Error) {
					xmlLoadError = e;
				}
			}
			
			override protected function createChildren():void {
				
				resourceManager.localeChain = ["en_US", "es_ES"]; //TODO: pull locale from the xml.
				
				super.createChildren();
				
				if(!xmlLoadError) {
					themeColor = courseware.color;
					
					topBar.searchPrompt = "Search " + courseware.product.name;
					topBar.hasSearch = !User.instance.lms;
					
					topBar.courses = courseware.product.courses;
				} else {
					var e:Error = xmlLoadError;
					var log:LogService = new LogService();
					
					log.dispatch('XML Load Failure: ' + e.toString() + '\n' + e.getStackTrace());
					
					themeColor = 0xa8a8a8;
					popupErrorDialog('There was an error loading data from the server.\n\nPlease contact Apex Technical Support at 1-866-294-4599 x111.');
				}
				
			}
			
			protected function maximize(event:FullScreenEvent):void {
				currentState = event.fullScreen ? "maximized" : "default";
			}

			protected function onAdded():void {
				stage.addEventListener(FullScreenEvent.FULL_SCREEN, maximize);
			}

			protected function popupErrorDialog(info:String, title:String = 'Error'):void {
				if (!currentlyIdle) {
					if (errorDialog.isPopUp) PopUpManager.removePopUp(errorDialog);	// Take down any currently displayed dialogs
					
					PopUpManager.addPopUp(errorDialog, this, true);
					PopUpManager.centerPopUp(errorDialog);
					infoText.text = info;
					errorDialog.title = title;
				}
			}

			private function applicationCompleteHandler():void {
				loaderInfo.uncaughtErrorEvents.addEventListener(UncaughtErrorEvent.UNCAUGHT_ERROR, uncaughtErrorHandler);
			}
			
			private function uncaughtErrorHandler(event:UncaughtErrorEvent):void {
				event.stopPropagation();
				
				var log:LogService = new LogService();
				
				if (event.error is Error) {
					var error:Error = event.error as Error;
					
					log.dispatch('UNHANDLED ERROR: ' + error.message + '\n\n' + error.getStackTrace());
					
					//popupErrorDialog('We are sorry, but there has been a problem and the application must be closed.\n\n' + error.message + '\n\nPlease contact Apex Technical Support at 1-866-294-4599 x111.');
				} else if (event.error is ErrorEvent) {
					var errorEvent:ErrorEvent = event.error as ErrorEvent;
					errorEvent.stopPropagation();
					log.dispatch('UNHANDLED EVENT: ' + errorEvent.type + ': ' + errorEvent.text);
									
					//popupErrorDialog('We are sorry, but there has been a problem and the application must be closed.\n\n' + errorEvent.type + '\n\nPlease contact Apex Technical Support at 1-866-294-4599 x111.');
				} else {
					// a non-Error, non-ErrorEvent type was thrown and uncaught
				}
			}
		

			protected function pageLoadErrorHandler(event:ErrorEvent):void {
				event.stopPropagation();
				//popupErrorDialog("We are sorry, but there has been an error loading this page.  Please contact Apex Technical Support at 1-866-294-4599 x111. \n\n" + event.text, "Error Loading Page");
				//Courseware.log("PAGE LOAD ERROR: " + event.type + ": " + event.text);
			}

		]]>
	</fx:Script>
	
	<transwarp:states>
		<s:State name="default" />
		<s:State name="maximized" />
	</transwarp:states>
	
	<!--Setting this group's width to "100%" resulted in it being 10,000 px wide instead of what was expected.-->
	<s:VGroup width="{width}" height="100%" gap.default="-53" gap.maximized="-35">
		<ui:TopBar id="topBar"/>
		<ui:LeftBar id="leftBar"/>		
	</s:VGroup>
	
	<!--The weird size constraints on this group are another case of having to force the layout code to behave as expected-->
	<ui:ContentArea id="contentArea" left="{leftBar.width}"
			  top="178" top.maximized="35"
			  width="{width-contentArea.x}" height="{height-contentArea.y}" 
			  error="pageLoadErrorHandler(event)"/>
	
	<!-- Drawing canvas -->
	<s:SpriteVisualElement id="canvas" left="0" top="0" right="0" bottom="0" cacheAsBitmap="true"/>	
	
</transwarp:TranswarpBase>