<?xml version="1.0" encoding="utf-8"?>
<dialogs:DialogBox xmlns:fx="http://ns.adobe.com/mxml/2009" 
			  xmlns:s="library://ns.adobe.com/flex/spark" 
			  xmlns:mx="library://ns.adobe.com/flex/mx"
			  xmlns:ui="com.apexinnovations.transwarp.ui.*"
			  xmlns:dialogs="com.apexinnovations.transwarp.ui.dialogs.*"
			  title="{resourceManager.getString('Dialogs','LINKS_TITLE')}"
			  addedToStage="addedToStage(event);" contentCreationComplete="contentCreationComplete(event);">
	<fx:Script>
		<![CDATA[
			import com.apexinnovations.transwarp.data.Courseware;
			import com.apexinnovations.transwarp.data.Link;
			import com.apexinnovations.transwarp.data.Page;
			import com.apexinnovations.transwarp.events.PageSelectionEvent;
			import com.apexinnovations.transwarp.ui.dialogs.LinkButton;
			import com.apexinnovations.transwarp.utils.TranswarpVersion;
			
			TranswarpVersion.revision = "$Rev$";

			import mx.managers.PopUpManager;
			
			protected var _page:Page = null;
			
			public var anchor:DisplayObject = null;
			
			[Bindable] public function get page():Page { return _page; }
			public function set page(p:Page):void {
				_page = p;
				updateElementList();
			}
			
			override protected function addedToStage(event:Event):void {
				super.addedToStage(event);
				Courseware.instance.addEventListener(PageSelectionEvent.PAGE_SELECTION_CHANGED, pageChanged);
				pageChanged(new PageSelectionEvent(Courseware.instance.currentPage));
			}
			
			protected function contentCreationComplete(event:Event):void {
				updateElementList();
			}
			
			override public function display():void {
				if (!visible && anchor) {
					x = anchor.x;
					y = anchor.y + 336;
				}
				visible = true;
				PopUpManager.bringToFront(this);
				setFocus();
			}
			
			protected function pageChanged(event:PageSelectionEvent):void {
				page = event.page as Page;
			}
			
			protected function updateElementList():void {
				if (elementList) {
					elementList.removeAllElements();
					
					if(page) {
						for each(var l:Link in _page.links) {
							var btn:LinkButton = new LinkButton();
							elementList.addElement(btn);
							btn.link = l;
						}
					}
				}
				currentState = (elementList && elementList.numElements) ? "default" : "no_results";
			}
		]]>
	</fx:Script>
	
	<dialogs:states>
		<s:State name="default" />
		<s:State name="no_results" />
	</dialogs:states>
	
	<s:VGroup paddingLeft="10" paddingRight="10" paddingTop="10" paddingBottom="10" >
		<ui:PageTitle />
		<s:Label width="380" fontSize="13" fontWeight="bold" textAlign="center" color="0xFF0000" paddingTop="10" paddingBottom="10" includeIn="no_results"
				 text="{resourceManager.getString('Dialogs','NO_LINKS')}"/>
		
		<s:Scroller horizontalScrollPolicy="off" maxHeight="300" visible.no_results="false" height.no_results="0" >
			<s:VGroup id="elementList" width="100%" paddingTop="5" paddingBottom="5" paddingLeft="5" paddingRight="5" />
		</s:Scroller>
		
		<s:Label fontStyle="italic" width="380" textAlign="right" color="0x0000FF" includeIn="default"
				 text="{resourceManager.getString('Dialogs','CLICK_LINKS_TO_VIEW')}"/>
	</s:VGroup>
</dialogs:DialogBox>
