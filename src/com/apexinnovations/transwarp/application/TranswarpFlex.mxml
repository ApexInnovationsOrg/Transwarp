<?xml version="1.0" encoding="utf-8"?>
<application:TranswarpPrototype xmlns:fx="http://ns.adobe.com/mxml/2009"
						 xmlns:s="library://ns.adobe.com/flex/spark"
						 xmlns:mx="library://ns.adobe.com/flex/mx"
						 xmlns:application="com.apexinnovations.transwarp.application.*"
						 xmlns:ui="com.apexinnovations.transwarp.ui.*"
						 preloader="com.apexinnovations.transwarp.application.preloader.CustomPreloader"
						 frameRate="30"  >

	<fx:Script>
		<![CDATA[
			import com.apexinnovations.transwarp.application.events.PageSelectionEvent;
			import com.apexinnovations.transwarp.config.IConfigurationAcceptor;
			import com.apexinnovations.transwarp.webservices.*;
			
			import flash.events.TimerEvent;
			import flash.utils.Timer;
			
			import flashx.textLayout.elements.IConfiguration;
			
			import mx.collections.XMLListCollection;
			
			
			
			protected var manager:CustomSystemManager;
			protected var loader:Loader;
			protected var visitTimer:Timer = new Timer(1000);
			protected var visit:PageService = new PageService();
			protected static var visitSession:uint = 0;
			
			[Bindable]
			protected var DEBUG:Boolean = true;
			
			protected override function createChildren():void {
				super.createChildren();
				manager = CustomSystemManager(systemManager);
				
				leftBar.productData = manager.xml.product.courses[0];

				container.addEventListener(Event.COMPLETE, loadComplete);
				container.addEventListener(IOErrorEvent.IO_ERROR, loadError);
				
				container.mask = contentMask;
			}

			protected function pageSelectionChanged(event:PageSelectionEvent):void {
				// stop any old timers
				visitTimer.stop();
				
				// update the current visit
				if (visitSession > 0) {
					visit.dispatch(ApexWebService.userID, ApexWebService.courseID, ApexWebService.pageID, visitSession);					
				}
				
				// set the new pageID
				ApexWebService.pageID = uint(event.id);
				
				// stop current page from running and load new page
				var page:XML = manager.xml.product.courses..page.(@id==event.id)[0];
				SoundMixer.stopAll();
				container.unloadAndStop();
				container.load(getURL(page.@id));

				// Allow 5 seconds to begin saving a new visit
				visitTimer.delay = 5000;
				visitTimer.addEventListener(TimerEvent.TIMER, timerHandler);
				visitTimer.start();
			}
			
			protected function timerHandler(event:TimerEvent):void {
				// stop the timer
				visitTimer.stop();
				
				if (visitTimer.delay == 5000 || visitSession == 0) {
					// create new visitSession
					visitSession = 0;
					visit.addEventListener(ApexWebServiceEvent.PAGE_COMPLETE, visitSaved);
					visit.addEventListener(ApexWebServiceEvent.PAGE_FAILURE, visitFailed);
					visit.dispatch(ApexWebService.userID, ApexWebService.courseID, ApexWebService.pageID);					
				} else {
					// update current session
					visit.dispatch(ApexWebService.userID, ApexWebService.courseID, ApexWebService.pageID, visitSession);					
				}
				
				// restart timer with delay of 1 minute
				visitTimer.delay = 60000;
				visitTimer.start();
			}

			protected function visitSaved(event:ApexWebServiceEvent):void {
				if (event.data.insertID) {
					visitSession = event.data.insertID;
				}
			}
			
			protected function visitFailed(event:ApexWebServiceEvent):void {
				// TODO: How are we handling this?
			}
			
			protected function loadComplete(event:Event):void {
				var content:DisplayObject = container.content;
				if(content is IConfigurationAcceptor)
					IConfigurationAcceptor(content).config = <div>woah, brah</div>;
				
			}
			
			protected function loadError(event:Event):void {
				var log:LogService = new LogService();
				log.dispatch(ApexWebService.userID, ApexWebService.courseID, ApexWebService.pageID, "Unable to load page content: " + getURL(ApexWebService.pageID));
			}

			private function getURL(number:int):String {
				return manager.xml.@rootFolder + '/PAGE_' + zeroPad(number, 6) + '.swf';
			}
			
			private function zeroPad(number:int, width:int):String {
				var s:String = '' + number;
				while(s.length < width) s = '0' + s;
				return s;
			}
		]]>
	</fx:Script>	
	
	
	<application:layout>
		<ui:TranswarpLayout />
	</application:layout>
	
	<s:VGroup width="100%" height="100%" gap="0">
		<ui:TopBar depth="1" />
		<s:Group depth="0" width="100%" height="100%">
			<mx:SWFLoader id="container" left="20" right="40" bottom="0" top="0" trustContent="{!DEBUG}"/>
			<ui:LeftBar id="leftBar" x="0" top="5" bottom="5" pageSelectionChanged="pageSelectionChanged(event)"/>
			<ui:RightBar id="rightBar" right="0" />
			
			<s:Group id="contentMask"  top="0" bottom="0" left="19" right="39">
				<s:Rect height="100%" width="100%">
					<s:fill>
						<s:SolidColor color="0x00ffff" alpha="0"/>
					</s:fill>
				</s:Rect>
			</s:Group>
		</s:Group>
	</s:VGroup>
	
</application:TranswarpPrototype>
